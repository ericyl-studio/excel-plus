plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.ericyl.excel'
version = '0.1.15-SNAPSHOT'

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // 从 java 组件中自动捕获项目的artifact
            from components.java

            // 可选：配置pom文件的生成，增加项目描述和开发者信息等
            pom {
                name = 'Excel Plus'
                description = 'Excel Plus'
                url = 'https://www.ericyl.com/excel-plus'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ericyl'
                        name = 'Ericyl Studio'
                        email = 'ericyl@ericyl.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ericyl-studio/excel-plus.git'
                    developerConnection = 'scm:git:ssh://github.com/ericyl-studio/excel-plus.git'
                    url = 'http://github.com/ericyl-studio/excel-plus'
                }
            }
        }
    }

    repositories {
        // 新的Central Portal发布配置
        maven {
            name = "CentralPortal"
            // 对于正式版本
            // url = "https://central.sonatype.com/api/v1/publisher"
            // 对于SNAPSHOT版本
            url = "https://central.sonatype.com/repository/maven-snapshots/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/ericyl-studio/excel-plus"
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
}

dependencies {
    // Lombok是一个注解处理器，只在编译时使用，不需要在运行时使用
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")

    // 这些依赖是库的API的一部分，会暴露给使用者
    api("org.apache.poi:poi:${poiVersion}")
    api("org.apache.poi:poi-ooxml:${poiVersion}")
    api("org.apache.commons:commons-lang3:${lang3Version}")
    api("org.apache.commons:commons-collections4:${collectionsVersion}")

    // 这些依赖是库的实现细节，不会暴露给使用者
    // implementation

    // 测试依赖
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

